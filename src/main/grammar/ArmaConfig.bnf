{
    parserClass="arma.config.parser.ArmaConfigParser"
    psiPackage="arma.config.psi"
    psiImplPackage="arma.config.psi.impl"
    elementTypeHolderClass="arma.config.psi.ArmaConfigTypes"
    elementTypeClass="arma.config.psi.ArmaConfigElementType"
    tokenTypeClass="arma.config.psi.ArmaConfigTokenType"

    tokens = [
        LBRACE      = '{'
        RBRACE      = '}'
        LBRACKET    = '['
        RBRACKET    = ']'
        EQUAL       = '='
        SEMICOLON   = ';'
        COMMA       = ','
        COLON       = ':'
        LPAREN      = '('
        RPAREN      = ')'
        PLUS        = '+'
        MINUS       = '-'
        STAR        = '*'
        SLASH       = '/'
        PERCENT     = '%'
        CARET       = '^'
        LT          = '<'
        GT          = '>'
        EXCL        = '!'
        SINGLE_HASH = '#'
        DOUBLE_HASH = '##'


        CLASS_KEYWORD   = 'class'
        DELETE_KEYWORD  = 'delete'
        MIN_KEYWORD     = 'min'
        MAX_KEYWORD     = 'max'

        LINE_COMMENT    = "LINE_COMMENT"
        BLOCK_COMMENT   = "BLOCK_COMMENT"
        PREPROCESSOR    = "PREPROCESSOR"
        TEXT            = "TEXT"
        FLOAT           = "FLOAT"
        NUMBER          = "NUMBER"
        STRING          = "STRING"
        SINGLE_QUOTE    = "SINGLE_QUOTE"

        // Char names can be based on https://www.ascii-code.com/
        // Stuff after = is basically just documentation/suggestion on what they are and doesnt do anything
    ]
}


root ::= item*

private item ::= macroBlock SEMICOLON
               | classBlock
               | deleteBlock
               | PREPROCESSOR
               | TEXT SEMICOLON
               | LINE_COMMENT
               | BLOCK_COMMENT

///////// MACRO /////////

private macroElement ::= LBRACE
                       | RBRACE
                       | LBRACKET
                       | RBRACKET
                       | EQUAL
                       | SEMICOLON
                       | COLON
                       | PLUS
                       | MINUS
                       | STAR
                       | SLASH
                       | PERCENT
                       | CARET
                       | GT
                       | LT
                       | EXCL
                       | SINGLE_HASH
                       | DOUBLE_HASH
                       | CLASS_KEYWORD
                       | DELETE_KEYWORD
                       | MIN_KEYWORD
                       | MAX_KEYWORD
                       | BLOCK_COMMENT
                       | TEXT
                       | FLOAT
                       | NUMBER
                       | STRING
                       | SINGLE_QUOTE
                       | COMMA

private macroAtom ::= macroElement | LPAREN macroAtom+ RPAREN

macroBlock ::= TEXT LPAREN macroAtom+ RPAREN

///////// CLASS /////////

classBody ::= LBRACE classItem* RBRACE
{pin=1}

classBlock ::= CLASS_KEYWORD className classExtension? classBody? SEMICOLON
{pin=1}

className ::= macroBlock
            | TEXT

classExtension ::= COLON className


private classItem ::= parameterBlock
                    | arrayBlock
                    | item

///////// DELETE /////////

deleteBlock ::= DELETE_KEYWORD className SEMICOLON

///////// PARAMETER /////////

parameterValue ::= macroBlock
                 | mathBlock
                 | TEXT
                 | STRING
                 | SINGLE_QUOTE
                 | NUMBER
                 | FLOAT

parameterName ::= macroBlock
                | TEXT


parameterBlock ::= parameterName EQUAL parameterValue SEMICOLON
{pin=2}

///////// ARRAY /////////

arrayBlock ::= parameterName LBRACKET RBRACKET PLUS? EQUAL LBRACE (parameterValue (COMMA parameterValue)*)? RBRACE SEMICOLON
{pin=3}

///////// MATH /////////

mathBlock ::= MINUS? mathElement (mathOperator mathElement)*

mathOperator ::= PLUS
               | MINUS
               | SLASH
               | STAR
               | PERCENT
               | MIN_KEYWORD
               | MAX_KEYWORD
               | CARET
               | GT
               | LT

mathElement ::= TEXT
              | NUMBER
              | FLOAT
              | LPAREN mathBlock RPAREN
