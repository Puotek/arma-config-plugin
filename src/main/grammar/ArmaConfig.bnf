{
  parserClass="arma.config.parser.ArmaConfigParser"
  psiPackage="arma.config.psi"
  psiImplPackage="arma.config.psi.impl"
  elementTypeHolderClass="arma.config.psi.ArmaConfigTypes"
  elementTypeClass="arma.config.psi.ArmaConfigElementType"
  tokenTypeClass="arma.config.psi.ArmaConfigTokenType"

    tokens = [
        CLASS_KEYWORD  = 'class'
        DELETE_KEYWORD = 'delete'
        LBRACE         = '{'
        RBRACE         = '}'
        LBRACKET       = '['
        RBRACKET       = ']'
        EQUAL          = '='
        SEMICOLON      = ';'
        COMMA          = ','
        COLON          = ':'
        LPAREN       = '('
        RPAREN       = ')'

        LINE_COMMENT  = 'regex://.*'
        BLOCK_COMMENT = 'regex:/\\*([^*]|\\*+[^*/])*\\*+/'
        PREPROCESSOR  = 'regex:#.*'

        IDENT  = "regex:[A-Za-z_][A-Za-z0-9_]*"
        FLOAT  = "regex:\\d+\\.\\d+"
        NUMBER = "regex:\\d+"
        STRING = "regex:\"([^\"\\\\n]|\\\\.)*\""
    ]
}

configFile ::= item*

private item ::= classDecl
               | classForwardDecl
               | assignment
               | deleteStmt
               | macroStmt
               | ';'

classDecl ::= CLASS_KEYWORD IDENT classExt? LBRACE item* RBRACE SEMICOLON?

classForwardDecl ::= CLASS_KEYWORD IDENT classExt? SEMICOLON

classExt ::= COLON className

className ::= IDENT | macroInvocation

assignment ::= IDENT arraySuffix? EQUAL value SEMICOLON

arraySuffix ::= LBRACKET RBRACKET

deleteStmt ::= DELETE_KEYWORD IDENT SEMICOLON

value ::= IDENT
        | FLOAT
        | NUMBER
        | STRING
        | array
        | macroInvocation

array ::= LBRACE valueList? RBRACE
valueList ::= value (COMMA value)*

macroInvocation ::= IDENT LPAREN macroInner? RPAREN

macroInner ::= macroInnerToken*

macroInnerToken ::= IDENT
                  | NUMBER
                  | FLOAT
                  | STRING
                  | PREPROCESSOR
                  | LBRACE | RBRACE
                  | LBRACKET | RBRACKET
                  | COMMA
                  | EQUAL
                  | COLON
                  | LINE_COMMENT
                  | BLOCK_COMMENT

macroStmt ::= macroInvocation SEMICOLON
            | IDENT SEMICOLON
